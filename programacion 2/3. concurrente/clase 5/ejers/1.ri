programa ejer1
procesos
  proceso incrementar( ES x : numero )
  comenzar
    x := ( x + 1 )
  fin

  proceso girar( ES direccionActual : numero ; E giros : numero )
  comenzar
    repetir( giros )
      derecha
      si( direccionActual < 3 )
        incrementar( direccionActual )
      sino
        direccionActual := 0
  fin

  proceso tomarFlores( ES cantFlores : numero )
  comenzar
    mientras( HayFlorEnLaEsquina )
      tomarFlor
      incrementar( cantFlores )
  fin

  proceso tomarFloresLinea( E largo : numero ; E direccionActual : numero ; ES cantFlores : numero )
  variables
    AvAnterior : numero
    CaAnterior : numero
  comenzar
    si( direccionActual = 0 )
      repetir( largo )
        tomarFlores( cantFlores )
        AvAnterior := PosAv
        CaAnterior := PosCa
        BloquearEsquina( PosAv , (PosCa + 1) )
        mover
        LiberarEsquina( AvAnterior , CaAnterior )
    sino
      si( direccionActual = 1 )
        repetir( largo )
          tomarFlores( cantFlores )
          AvAnterior := PosAv
          CaAnterior := PosCa
          BloquearEsquina( (PosAv + 1) , PosCa )
          mover
          LiberarEsquina( AvAnterior , CaAnterior )
      sino
        si( direccionActual = 2 )
          repetir( largo )
            tomarFlores( cantFlores )
            AvAnterior := PosAv
            CaAnterior := PosCa
            BloquearEsquina( PosAv , (PosCa - 1) )
            mover
            LiberarEsquina( AvAnterior , CaAnterior )
        sino
          repetir( largo )
            tomarFlores( cantFlores )
            AvAnterior := PosAv
            CaAnterior := PosCa
            BloquearEsquina( (PosAv - 1) , PosCa )
            mover
            LiberarEsquina( AvAnterior , CaAnterior )
  fin

  proceso tomarFloresPerimetro(  E alto : numero ; E ancho : numero ; ES direccionActual : numero ; ES cantFlores : numero )
  comenzar
    tomarFloresLinea( alto , direccionActual , cantFlores )
    girar( direccionActual , 1 )

    tomarFloresLinea( ancho , direccionActual , cantFlores )
    girar( direccionActual , 1 )

    tomarFloresLinea( alto , direccionActual , cantFlores )
    girar( direccionActual , 1 )

    tomarFloresLinea( ancho , direccionActual , cantFlores )
  fin

  proceso decrementar( ES x : numero )
  comenzar
    x := ( x - 1 )
  fin

  proceso dejarFlor( ES cantFlores : numero )
  comenzar
    si( HayFlorEnLaBolsa )
      dejarFlor
      decrementar( cantFlores )
  fin

  proceso dejarFlores( ES cantFlores : numero )
  comenzar
    mientras( HayFlorEnLaBolsa )
      depositarFlor
      decrementar( cantFlores )
  fin

areas
  oficina   : AreaP ( 1  , 1  , 1  , 1  )
  floreria1 : AreaP ( 2  , 2  , 2  , 2  )
  floreria2 : AreaP ( 6  , 6  , 6  , 6  )
  area1     : AreaP ( 15 , 15 , 30 , 35 )
  area2     : AreaP ( 40 , 40 , 55 , 60 )
  deposito  : AreaP ( 10 , 10 , 10 , 10 )

robots
  robot florero
  variables
    id              : numero
    Av              : numero
    Ca              : numero
    AvInicial       : numero
    CaInicial       : numero
    alto            : numero
    ancho           : numero
    cantFlores      : numero
    direccionActual : numero
  comenzar
    cantFlores      := 0
    direccionActual := 0
    AvInicial       := PosAv
    CaInicial       := PosCa
    BloquearEsquina( AvInicial , CaInicial )
    RecibirMensaje( id    , jefe )
    RecibirMensaje( Av    , jefe )
    RecibirMensaje( Ca    , jefe )
    RecibirMensaje( alto  , jefe )
    RecibirMensaje( ancho , jefe )
    BloquearEsquina( Av , Ca )
    Pos( Av , Ca )
    LiberarEsquina( AvInicial , CaInicial )
    tomarFloresPerimetro( alto , ancho , direccionActual , cantFlores )
    EnviarMensaje( id         , jefe )
    EnviarMensaje( cantFlores , jefe )
    BloquearEsquina( 10 , 10 )
    Pos( 10 , 10)
    LiberarEsquina( Av , Ca )
    dejarFlores( cantFlores )
    BloquearEsquina( AvInicial , CaInicial )
    Pos( AvInicial , CaInicial )
    LiberarEsquina( 10 , 10 )
  fin

  robot TJefe
  variables
    Av            : numero
    Ca            : numero
    id            : numero
    alto          : numero
    ancho         : numero
    cantRobots    : numero
    flores        : numero
    floresTotales : numero
  comenzar
    cantRobots := 2
    id         := 1
    repetir( cantRobots )
      si( id = 1 )
        EnviarMensaje( id , florero1 )
        Av    := 15
        Ca    := 15
        alto  := 20
        ancho := 15
        EnviarMensaje( Av    , florero1 )
        EnviarMensaje( Ca    , florero1 )
        EnviarMensaje( alto  , florero1 )
        EnviarMensaje( ancho , florero1 )
      sino
        EnviarMensaje( id , florero2 )
        Av    := 40
        Ca    := 40
        alto  := 20
        ancho := 15
        EnviarMensaje( Av    , florero2 )
        EnviarMensaje( Ca    , florero2 )
        EnviarMensaje( alto  , florero2 )
        EnviarMensaje( ancho , florero2 )
      incrementar( id )
    floresTotales := 0
    id := 1
    repetir( cantRobots )
      RecibirMensaje( id , * )
      si( id = 1 )
        RecibirMensaje( flores , florero1 )
      sino
        RecibirMensaje( flores , florero2 )
      floresTotales := ( floresTotales + flores )
    Informar( 'flores' , floresTotales )
  fin

variables
  jefe     : TJefe
  florero1 : florero
  florero2 : florero

comenzar
  AsignarArea( jefe     , oficina   )
  AsignarArea( florero1 , floreria1 )
  AsignarArea( florero2 , floreria2 )
  AsignarArea( florero1 , area1     )
  AsignarArea( florero2 , area2     )
  AsignarArea( florero1 , deposito  )
  AsignarArea( florero2 , deposito  )
  Iniciar( jefe     , 1 , 1 )
  Iniciar( florero1 , 2 , 2 )
  Iniciar( florero2 , 6 , 6 )
fin